<%args>
$Month => (localtime)[4]
$Year => (localtime)[5] + 1900
$Query => undef
$Format => undef
$Order => undef
$OrderBy => undef
$RowsPerPage => undef
$NewQuery => 0
$WeekDay => undef
$WeekMonth => undef
$WeekYear => undef
$OrigMonth => undef
$OrigYear => undef
</%args>

<& /Elements/Header, Title => $title &>
<& /Elements/Tabs &>

% my $title;
% if ( $WeekMonth ) {
%   if ( $date->month == $end->month ) {
%     $title = $rtdate->GetMonth( $date->month -1 ). ' '.
%              $date->day. '-'. $end->day. ', '. $date->year;
%   } elsif ( $date->year == $end->year ) {
%     $title =
%       $rtdate->GetMonth( $date->month -1 ). ' '. $date->day. ' - '.
%       $rtdate->GetMonth( $end->month  -1 ). ' '. $end->day.  ', '. $end->year;
%   } else {
%     $title =
%       $rtdate->GetMonth( $date->month -1 ). ' '.$date->day. ', '. $date->year.
%       ' - '.
%       $rtdate->GetMonth( $end->month  -1 ). ' '.$end->day.  ', '. $end->year;
%   }
% } else {
%   $title = $rtdate->GetMonth($Month) . " $Year" 
% }
<&| /Widgets/TitleBox,
     title => loc('Calendar for '). $title,
     title_class=> 'inverse',
     color => "#993333" &>

% my($PMonth, $PYear, $NMonth, $NYear);
% unless ( $WeekDay ) {

    <table width="100%">
      <tr>
        <td align="left">
% ($PMonth, $PYear) = ($Month - 1, $Year);
% if ($PMonth < 0) {
%    $PYear--;
%    $PMonth = 11;
% }
          <a href="<%$RT::WebPath%>/Search/Calendar.html?Month=<%$PMonth%>&Year=<%$PYear%>&<%$QueryString%>">«<%$rtdate->GetMonth($PMonth)%></a>
        </td>
        <th align="center">
          <font size="+1"><% $rtdate->GetMonth($Month). " $Year" %></font>
        </th>
        <td align="right">
% ($NMonth, $NYear) = ($Month + 1, $Year);
% if ($NMonth > 11) {
%    $NYear++;
%    $NMonth = 0;
% }
          <a href="<%$RT::WebPath%>/Search/Calendar.html?Month=<%$NMonth%>&Year=<%$NYear%>&<%$QueryString%>"><%$rtdate->GetMonth($NMonth)%>»</a>
        </td>
      </tr>
    </table>
% }

<table class="rtxcalendar">

<thead>
<tr>
<td class="controls"></td>
% for ( @{$week{$weekstart}} ) {
<th width="14%"><%$rtdate->GetWeekday($_)%></th>
% }
</tr>
</thead>

<tbody>
<tr>
% if ( $WeekDay ) {
  <& td_week_collapse, date=>$date, Month=>$OrigMonth, Year=>$OrigYear, QueryString=>$QueryString &>
% } else {
  <& td_week_expand, date=>$date, Month=>$Month, Year=>$Year, QueryString=>$QueryString &>
% }
% while ($date <= $end) {
%
%   my $offmonth = !$WeekDay && $date->month != ($Month + 1);
%   my $is_today     = (DateTime->compare($today,     $date) == 0);
%   my $is_yesterday = (DateTime->compare($yesterday, $date) == 0);
%   my $is_aweekago  = (DateTime->compare($aweekago,  $date) == 0);

    <td class="<%   $offmonth     ? 'offmonth'
                  : $is_today     ? 'today'
                  : $is_yesterday ? 'yesterday'
                  : $is_aweekago  ? 'aweekago'
                  : ''
               %>"
    >
      <div class="<%   $is_today ? 'todays'
                     : $offmonth ? 'offmonth'
                     : ''
                  %>calendardate"
      ><% ( $WeekDay ? $rtdate->GetMonth($date->month-1). ' ' : '' ).
          $date->day
       %></div>

%     my $sp = 3;
%     for my $t ( @{ $Tickets{$date->strftime("%F")} } ) {
%       $sp--;
        <& /Elements/CalendarEvent, Object => $t, Date => $date, DateTypes => \%DateTypes &>
%     }
      <% ($sp>0) ? '<BR>'x$sp : '' |n %>

    </td>

%   $date = $set->next($date);
%   if ( $date->day_of_week == $startday_of_week ) {
      </tr>
%     if ( $date <= $end ) {
        <tr>
%         if ( $WeekDay ) {
            <& td_week_collapse, date=>$date, Month=>$OrigMonth, Year=>$OrigYear, QueryString=>$QueryString &>
%         } else {
            <& td_week_expand, date=>$date, Month=>$Month, Year=>$Year, QueryString=>$QueryString &>
%         }
%     }
%   }

% }
</tbody>
</table>

% unless ( $WeekDay ) {

    <table width="100%">
      <tr>
        <td align="left">
          <a href="<%$RT::WebPath%>/Search/Calendar.html?Month=<%$PMonth%>&Year=<%$PYear%>&<%$QueryString%>">«<%$rtdate->GetMonth($PMonth)%></a>
        </td>

        <td valign="top" align="center">
          <form action="<%$RT::WebPath%>/Search/Calendar.html?<%$QueryString%>" method="post">

            <select name="Month">
%             for (0..11) {
                <option value="<%$_%>" <% $_ == $Month ? 'selected' : ''%> ><%$rtdate->GetMonth($_)%></option>
%             }
            </select>

%           my $year = (localtime)[5] + 1900;
            <select name="Year">
%             for ( ($year-5) .. ($year+5)) {
                <option value="<%$_%>" <% $_ == $Year ? 'selected' : ''%>><%$_%></option>
%             }
            </select>

%# <& /Elements/Submit&>
            <input type="submit" value="<% loc('Submit') %>" class="button" />

          </form>
        </td>

        <td align="right">
          <a href="<%$RT::WebPath%>/Search/Calendar.html?Month=<%$NMonth%>&Year=<%$NYear%>&<%$QueryString%>"><%$rtdate->GetMonth($NMonth)%>»</a>
        </td>
      </tr>
  </table>

% }

% #XXX an option to turn off "Calendar Preferences and Help" for embedded
% # (and weekly?) use
<table width="100%">
<tr>

<td valign="top" rowspan=9>
  <BR>
  <a href="<%$RT::WebPath%>/Prefs/Calendar.html">Calendar Preferences and Help</a>
</td>

% foreach my $legend (keys %legend) {
    <tr>
      <td align="right">
        <img src="<%$RT::WebImagesURL%>/<%$legend%>.png" />
      </td>
      <td align="left">
%       my $more = 0;
%       foreach ( @{$legend{$legend}} ) {
          <% $more++ ? ', ' : '' %>
          <&|/l&><% $_ %></&>
%       }
      </td>
    </tr>
% }

</table>

</&>

<%ONCE>

my %legend = (
  'created'     => ['Created'],
  'due'         => ['Due'],
  'resolved'    => ['Resolved'],
  'updated'     => ['Last Updated'],
  'created_due' => ['Created','Due'],
  'reminder'    => ['Reminders'],
  'started'     => ['Started'],
  'starts_due'  => ['Starts','Due'],
);

</%ONCE>
<%INIT>
use RTx::Calendar qw(FirstDay LastDay LastDayOfWeek);

my $title = loc("Calendar");

my @DateTypes = qw/Created Starts Started Due LastUpdated Resolved/;

my $rtdate = RT::Date->new($session{'CurrentUser'});

my $weekstart = 'Sunday'; #RT::SiteConfig?  user pref?
my %week = (
  'Saturday' => [6,0..5],
  'Sunday'   => [0..6],
  'Monday'   => [1..6,0],
);
my $startday_of_week = ${$week{$weekstart}}[0]  || 7;
my $endday_of_week   = ${$week{$weekstart}}[-1] || 7;

my $today = DateTime->today;
my $yesterday = $today->clone->subtract( days=>1 );
my $aweekago  = $today->clone->subtract( days=>7 );

my( $date, $end );
if ( $WeekDay ) {
  $date = DateTime->new( year=>$WeekYear, month=>$WeekMonth+1, day=>$WeekDay );
  $end  = LastDayOfWeek( $WeekYear, $WeekMonth+1, $WeekDay, $endday_of_week );
} else {
  $date = FirstDay($Year, $Month + 1, $startday_of_week );
  $end  = LastDay ($Year, $Month + 1, $endday_of_week );
}

# use this to loop over days until $end
my $set = DateTime::Set->from_recurrence(
    next => sub { $_[0]->truncate( to => 'day' )->add( days => 1 ) }
);

my $QueryString =
      $m->comp(
        '/Elements/QueryString',
        Query   => $Query,
        Format  => $Format,
        Order   => $Order,
        OrderBy => $OrderBy,
        Rows    => $RowsPerPage
      )
      if ($Query);

$QueryString ||= 'NewQuery=1';

# Default Query and Format
my $TempFormat = "__Starts__ __Due__";
my $TempQuery = "( Status = 'new' OR Status = 'open' OR Status = 'stalled')
 AND ( Owner = '" . $session{CurrentUser}->Id ."' OR Owner = 'Nobody'  )
 AND ( Type = 'reminder' OR 'Type' = 'ticket' )";

if ( my $Search = RTx::Calendar::SearchDefaultCalendar($session{CurrentUser}) ) {
  $TempFormat = $Search->SubValue('Format');
  $TempQuery = $Search->SubValue('Query');
}

# we overide them if needed
$TempQuery  = $Query  if $Query;
$TempFormat = $Format if $Format;

# we search all date types in Format string
my @Dates = grep { $TempFormat =~ m/__${_}(Relative)?__/ } @DateTypes;

# used to display or not a date in Element/CalendarEvent
my %DateTypes = map { $_ => 1 } @Dates;

$TempQuery .= RTx::Calendar::DatesClauses(\@Dates, $date->strftime("%F"), $end->strftime("%F"));

# print STDERR ("-" x 30), "\n", $TempQuery, "\n";

my %Tickets = RTx::Calendar::FindTickets($session{'CurrentUser'}, $TempQuery, \@Dates, $date->strftime("%F"), $end->strftime("%F"));

</%INIT>
<%def td_week_expand>
<%args>
  $date  => undef
  $Month => undef
  $Year  => undef
  $QueryString => undef
</%args>
  <td class="controls">
    <a href="<%$RT::WebPath%>/Search/Calendar.html?WeekDay=<% $date->day %>&WeekMonth=<% $date->month -1 %>&WeekYear=<% $date->year %>&OrigMonth=<% $Month %>&OrigYear=<% $Year %>&<%$QueryString%>"><img src="<%$RT::WebPath%>/NoAuth/images/week-expand.gif"></a>
  </td>
</%def>

<%def td_week_collapse>
<%args>
  $date  => undef
  $Month => undef
  $Year  => undef
  $QueryString => undef
</%args>
  <td class="controls">
    <a href="<%$RT::WebPath%>/Search/Calendar.html?Month=<% $Month %>&Year=<% $Year %>&<%$QueryString%>"><img src="<%$RT::WebPath%>/NoAuth/images/week-collapse.gif"></a>
  </td>
</%def>
