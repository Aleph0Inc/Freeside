<& elements/search.html,
          'title'       => emt('Service search results'),
          'name'        => emt('services'),
          'query'       => $sql_query,
          'count_query' => $count_query,
          'redirect'    => $link,
          'header'      => [ emt('#'),
                             emt('Service'),
                             emt('Pkg. Status'),
                             # package?
                             FS::UI::Web::cust_header(),
                           ],
          'fields'      => [ 'svcnum',
                             sub { 
                               #$_[0]->svc. ': '. $_[0]->label;
                               my($label, $value, $svcdb) = $_[0]->label;
                               my $id = $_[0]->agent_svcid
                                      ? $_[0]->agent_svcid.': '
                                      : '';
                               "$label: $id$value";
                             },
                             sub {
                               $cust_pkg_cache{$_[0]->svcnum} ||= $_[0]->cust_pkg;
                               return '' unless $cust_pkg_cache{$_[0]->svcnum};
                               $cust_pkg_cache{$_[0]->svcnum}->ucfirst_status
                             },
                             # package?
                             \&FS::UI::Web::cust_fields,
                           ],
          'links'       => [ $link,
                             $link,
                             '', # pkg status
                             # package?
                             ( map { $_ ne 'Cust. Status' ? $link_cust : '' }
                                 FS::UI::Web::cust_header()
                             ),
                           ],
          'align'       => 'rlr'. FS::UI::Web::cust_aligns(),
          'color'       => [ 
                             '',
                             '',
                             sub {
                               $cust_pkg_cache{$_[0]->svcnum} ||= $_[0]->cust_pkg;
                               return '' unless $cust_pkg_cache{$_[0]->svcnum};
                               my $c = FS::cust_pkg::statuscolors;
                               $c->{$cust_pkg_cache{$_[0]->svcnum}->status };
                             }, # pkg status
                             FS::UI::Web::cust_colors(),
                           ],
          'style'       => [ 
                             '',
                             '',
                             'b', # pkg status
                             FS::UI::Web::cust_styles(),
                           ],
&>
<%init>

die "access denied"
  unless $FS::CurrentUser::CurrentUser->access_right('List services');

my %cust_pkg_cache;

my $sql_query;

my $orderby = 'ORDER BY cust_svc.svcnum'; #has to be ordered by something
                                          #for pagination to work

if ( length( $cgi->param('search_svc') ) ) {

  $sql_query = {
    FS::cust_svc->smart_search_param(
     'search' => scalar($cgi->param('search_svc'))
    )
  };

} else {

  my $addl_from = ' LEFT JOIN part_svc  USING ( svcpart ) '.
                  ' LEFT JOIN cust_pkg  USING ( pkgnum  ) '.
                  FS::UI::Web::join_cust_main('cust_pkg', 'cust_pkg');

  my @extra_sql = ();

  if ( $cgi->param('magic') =~ /^(all|unlinked)$/ ) {

    $cgi->param('svcdb') =~ /^(svc_\w+)$/ or die "unknown svcdb";
    push @extra_sql, "svcdb = '$1'";
    $addl_from .= " LEFT JOIN $1 USING ( svcnum ) ";

    push @extra_sql, 'pkgnum IS NULL'
      if $cgi->param('magic') eq 'unlinked';

    if ( $cgi->param('sortby') =~ /^(\w+)$/ ) {
      my $sortby = $1;
      $orderby = "ORDER BY $sortby";
    }

  } elsif ( $cgi->param('svcpart') =~ /^(\d+)$/ ) {

    push @extra_sql, "svcpart = $1";
    if (defined($cgi->param('cancelled'))) {
      if ($cgi->param('cancelled')) {
        push @extra_sql, "cust_pkg.cancel IS NOT NULL";
      } else {
        push @extra_sql, "cust_pkg.cancel IS NULL";
      }
    }

  } else {
    errorpage("No search term specified");
  }

  #here is the agent virtualization
  push @extra_sql, $FS::CurrentUser::CurrentUser->agentnums_sql( 
                     'null_right' => 'View/link unlinked services'
                   );

  my $extra_sql = ' WHERE '. join(' AND ', @extra_sql );

  $sql_query = {
    'select'     => 'cust_svc.*, part_svc.*',
    'table'      => 'cust_svc',
    'addl_from'  => $addl_from,
    'hashref'    => {},
    'extra_sql'  => $extra_sql,
  };

}

# at this point the query must provide all fields from 
# cust_svc and part_svc, and must include join_cust_main.
$sql_query->{'select'} = join(', ',
                                    $sql_query->{'select'},
                                    'cust_main.custnum',
                                    FS::UI::Web::cust_sql_fields(),
                             );
$sql_query->{'order_by'} = $orderby;

my $count_query = "SELECT COUNT(*) FROM cust_svc ". $sql_query->{addl_from}.
                                               ' '. $sql_query->{extra_sql};

my $link = sub {
  my $cust_svc = shift;
  my $url;
  if ( $cust_svc->svcpart ) {
    $url = svc_url(
      'm'        => $m,
      'action'   => 'view',
      'svcdb'    => $cust_svc->svcdb, #we have it from the joined search
      'query'     => '',
    );
  } else { # bizarre unlinked service case
    $url = $p.'view/svc_Common.html?svcdb='.$cust_svc->svcdb.';svcnum=';
  }
  [ $url, 'svcnum' ];
};

my $link_cust = sub {
  my $cust_svc = shift;
  if ( $cust_svc->custnum ) {
    [ "${p}view/cust_main.cgi?", 'custnum' ];
  } else {
    '';
  }
};

</%init>
