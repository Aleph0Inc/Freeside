<DEL><% mt("Payment [_1] by [_2]", $info, $cust_pay_void->otaker ) |h %></DEL>
<I><% mt("voided [_1]", time2str($date_format, $cust_pay_void->void_date) ) |h %> 
% my $void_user = $cust_pay_void->void_access_user;
% if ($void_user) {
    by <% $void_user->username %></I>
% }
<% $unvoid %>
<%init>

my( $cust_pay_void, %opt ) = @_;

my $date_format = $opt{'date_format'} || '%m/%d/%Y';

my $curuser = $FS::CurrentUser::CurrentUser;

my $payby = $cust_pay_void->payby;

my $payinfo;
if ( $payby eq 'CARD' ) {
  $payinfo = $cust_pay_void->paymask;
} elsif ( $payby eq 'CHEK' ) {
  my( $account, $aba ) = split('@', $cust_pay_void->paymask );
  $payinfo = mt("ABA [_1], Acct #[_2]",$aba,$account);
} else {
  $payinfo = $cust_pay_void->payinfo;
}

$payby = translate_payby($payby,$payinfo);
my $info = $payby ? "($payby$payinfo)" : '';

if ( $opt{'pkg-balances'} && $cust_pay_void->pkgnum ) {
  my $cust_pkg = qsearchs('cust_pkg', { 'pkgnum' => $cust_pay_void->pkgnum } );
  $info .= ' for '. $cust_pkg->pkg_label_long;
}

my $unvoid = '';
my $unvoidmsg = $cust_pay_void->payby =~ /^(CARD|CHEK)$/
                ? ' ('.mt('do not send anything to the payment gateway') .')'
                : '';
$unvoid = areyousure_link("${p}misc/unvoid-cust_pay_void.cgi?".$cust_pay_void->paynum,
                            mt('Are you sure you want to unvoid this payment?'),
                            mt('Unvoid this payment from the database') . $unvoidmsg,
                            mt('unvoid')
                         )
    if ( $cust_pay_void->closed !~ /^Y/i && $curuser->access_right('Unvoid') );

</%init>
