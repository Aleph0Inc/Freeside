<& /elements/header-popup.html &>
  <SCRIPT TYPE="text/javascript">
    window.top.location.reload();
  </SCRIPT>
  </BODY>
</HTML>
<%init>

my $curuser = $FS::CurrentUser::CurrentUser;
die "access denied"
  unless $curuser->access_right('Change package start date');

$cgi->param('pkgnum') =~ /^(\d+)$/
  or die "illegal pkgnum";
my $pkgnum = $1;

my $cust_pkg = qsearchs({
  table     => 'cust_pkg',
  addl_from => ' JOIN cust_main USING (custnum) ',
  hashref   => { 'pkgnum' => $pkgnum },
  extra_sql => ' AND '. $curuser->agentnums_sql,
}) or die "Unknown pkgnum: $pkgnum";

my $cust_main = $cust_pkg->cust_main;

my $error;
my $start_date;
if ( $cgi->param('when') eq 'now' ) {
  # start it the next time billing runs
  $start_date = '';
} elsif ( $cgi->param('when') eq 'next_bill_date' ) {
  $start_date = $cust_main->next_bill_date;
} elsif ( $cgi->param('when') eq 'date' ) {
  $start_date = parse_datetime($cgi->param('start_date'));
}

if ( $cust_pkg->setup ) {
  # shouldn't happen
  $error = 'This package has already started billing.';
} else {
  local $FS::UID::AutoCommit = 0;
  foreach my $pkg ($cust_pkg, $cust_pkg->supplemental_pkgs) {
    $pkg->set('start_date', $start_date);
    $error ||= $pkg->replace;
  }
  $error ? dbh->rollback : dbh->commit;
}

if ( $error ) {
  $cgi->param('error', $error);
  print $cgi->redirect($fsurl.'misc/change_pkg_start.html?', $cgi->query_string);
}
</%init>
